_package user
$

def_slotted_exemplar(
	:geojson_tile_server_route,
	{
	},
	:abstract_tile_server_route)
$

_private _method geojson_tile_server_route.write_to_file(filename)
	_dynamic !print_float_precision! << 10
	
	map_view << .application.plugin(:maps).current_map_view
	file << external_text_output_stream.new(filename)
	
	# write response to file
	geoms << map_view.get_visible_geometry_set()
	_protect
		geoms << geometry_set.new_from(geoms)
		_dynamic !current_coordinate_system! << _self.cs_4326
		geoms.to_geojson(file)
	_protection
		file.close()
	_endprotect
_endmethod
$

_method geojson_tile_server_route.send_tile(response)
	tmp_file << system.temp_file_name("tile.geojson")
	_self.write_to_file(tmp_file)
	
	# send file
	ss << external_binary_input_stream.new(tmp_file)
	response.headers["Content-Type"] << "application/vnd.geo+json"
	response.send(200, ss)
	ss.close()
	
	system.unlink(tmp_file, _true)
_endmethod
$

_private _method geojson_tile_server_route.open_tile()
	tmp_file << system.temp_file_name("tile.geojson")
	_self.write_to_file(tmp_file)
	
	system.start_command("start " + tmp_file)
	#system.unlink(tmp_file, _true)
_endmethod
$
